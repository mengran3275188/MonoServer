stateTable=createTxtTable("./config/StateList.txt");
cfgTable=createConfigTable("./config/ServerConfigList.txt");
logFile=("./log/config_"+serverType+"_"+getLogFileId()+".log");
stateName="";

firstNodeJsId = 4;
firstRoomServerId = 20;
gmPortalId = 11;

function(log)
{
	print(""+$0);
	writeString(logFile,""+$0);
};

function(setState)
{
	stateName=$0;
	log("状态名："+$0);
};

function(handleIni)
{
	@iniFile = $0;
	@stateIni=createIniReader(@iniFile);
	@num=@stateIni.getInfoCount();
	for(@ix=0;@ix<@num;++@ix)
	{
		@section=@stateIni.getSection(@ix);
		@key=@stateIni.getKey(@ix);
		@value=@stateIni.getValue(@ix);
		if(@section!=""){
			stringMap.insert(@section+"$$"+@key,@value);
		}else{
			stringMap.insert(@key,@value);
		};
	};	
};

function(handleState)
{
	if(stateName!="")
	{
		@rIndex=stateTable.findRow(2,0,stateName);
		if(@rIndex>=2)
		{
			@colNum=stateTable.getColumnCount();
			for(@i=1;@i<@colNum;++@i)
			{
				@stateFile=stateTable.getCell(@rIndex,@i);
				if(@stateFile!="")
				{
					log("处理状态文件："+@stateFile);
					handleIni(@stateFile);
				};
			};
		};
	};
};

function(main)
{
	stringMap.initMap(1000);
	@ip0=readString("./etc/ip.txt",0);
	if(@ip0=="")
	{
		log("找不到当前机器IP！");		
	};
	log("当前机器外网IP："+@ip0);
	
	@rIndex=cfgTable.findByIP0(0,@ip0);
	if(@rIndex>=0)
	{
		log("当前机器配置行:"+@rIndex);
	}
	else
	{
		log("找不到当前机器配置！");
		return 0;
	};
	@sname=cfgTable.getServerName(@rIndex);
	log("当前SERVER名："+@sname);
	@sid=cfgTable.getServerID(@rIndex);
	@zid=cfgTable.getZoneID(@rIndex);
	@bid=cfgTable.getBigworldID(@rIndex);
	@wid=cfgTable.getWorldID(@rIndex);
	log("当前机器信息：[serverid,zoneid,bigworldid,worldid]:"+@sid+","+@zid+","+@bid+","+@wid);
	stringMap.insert("worldid",""+@wid);
	@rIndex=cfgTable.findByID(0,@zid,@bid,@bid,gmPortalId);

	//查找center配置
	@rIndex=cfgTable.findByID(0,@zid,@bid,@wid,0);
	if(@rIndex>=0)
	{
		log("center机器配置行:"+@rIndex);
	}
	else
	{
		log("找不到center机器配置！");
		return 0;
	};
	@ip1 = cfgTable.getIP1(@rIndex);
	@port = cfgTable.getValueByKey(@rIndex,"端口");
	if(serverType=="center"){
		stringMap.insert("ip","0.0.0.0");
	}else{
		log("set server ip");
		stringMap.insert("ip",@ip1);
	};
	stringMap.insert("port",@port);
	log("center ip/port:"+@ip1+"/"+@port);
		
	//查找store配置
	if(serverType=="database" || serverType=="gmserver"){	  
  	@rIndex=cfgTable.findByID(0,@zid,@bid,@wid,2);
  	if(@rIndex>=0)
  	{
  		log("store机器配置行:"+@rIndex);
  	}
  	else
  	{
  		log("找不到store机器配置！");
  		return 0;
  	};
  	@dbIp = cfgTable.getValueByKey(@rIndex,"DB");
		stringMap.insert("Server",@dbIp);
	};
	
	//加载对应类型服务器的配置
	handleIni("./config/DiffConf/"+serverType+".ini");
	//查找lobby配置
	@rIndex=cfgTable.findByID(0,@zid,@bid,@wid,1);
	if(@rIndex>=0)
	{
		log("lobby机器配置行:"+@rIndex);
	}
	else
	{
		log("找不到lobby机器配置！");
		return 0;
	};
	
	if(serverType=="gmserver"){
	  stringMap.insert("name","GmServer");
	};
	if(serverType=="center")
	{
		stringMap.insert("name","Center");
		@sid = toint(argv(0));
		if(@sid > 0)
		{
			// main center
			@brIndex=cfgTable.findByID(0,@zid,@bid,@bid,0);
			log(@brIndex);
			if(@brIndex >=0)
			{
				@bport = cfgTable.getValueByKey(@brIndex,"端口");
				stringMap.insert("port",@bport);
			};
			
		};
	};
	if(serverType=="lobby"){
		stringMap.insert("name","Lobby");
		@startServerTime=cfgTable.getValueByKey(@rIndex,"开服时间");
		stringMap.insert("StartServerTime",@startServerTime);
		@logVer=cfgTable.getValueByKey(@rIndex,"日志版本");
		stringMap.insert("LogNormVersion",@logVer);
		@shopMask=cfgTable.getValueByKey(@rIndex,"商店掩码");
		stringMap.insert("shopmask",@shopMask);	 	  
	  
		if(@bid!=@wid){
			//普通世界
			@brIndex=cfgTable.findByID(0,@zid,@bid,@bid,0);
			if(@brIndex>=0){
				@bip1=cfgTable.getIP1(@brIndex);
				@bport = cfgTable.getValueByKey(@brIndex,"端口");
				stringMap.insert("centernum","2");
				stringMap.insert("ip0",@ip1);
				stringMap.insert("port0",@port);
				stringMap.insert("worldid0",""+@wid);
				stringMap.insert("name0","Lobby");
				stringMap.insert("ip1",@bip1);
				stringMap.insert("port1",@bport);
				stringMap.insert("worldid1",""+@bid);
				stringMap.insert("name1","Lobby"+@wid);
			}else{
				stringMap.insert("centernum","1");
				stringMap.insert("ip0",@ip1);
				stringMap.insert("port0",@port);
				stringMap.insert("worldid0",""+@wid);
				stringMap.insert("name0","Lobby");
			};
		};
	};
	if(serverType=="bwlobby"){
		stringMap.insert("name","BWLobby");
		@brIndex=cfgTable.findByID(0,@zid,@bid,@bid,0);
		if(@brIndex>=0){
				@bip1=cfgTable.getIP1(@brIndex);
				@bport = cfgTable.getValueByKey(@brIndex,"端口");
				stringMap.insert("centernum","1");
				stringMap.insert("ip0",@bip1);
				stringMap.insert("port0",@bport);
				stringMap.insert("worldid0",""+@wid);
				stringMap.insert("name0","BWLobby");
		};
	};
	if(serverType=="battleserver"){
		stringMap.insert("name","BattleServer");
		
		@brIndex=cfgTable.findByID(0,@zid,@bid,@bid,0);
		@sid = toint(argv(0));
		@serverIndex=cfgTable.findByID(0,@zid,@bid,@bid,@sid);
		if(@brIndex>=0 && @serverIndex>=0){
				@bip1=cfgTable.getIP1(@brIndex);
				@bport = cfgTable.getValueByKey(@brIndex,"端口");
				stringMap.insert("centernum","1");
				stringMap.insert("ip0",@bip1);
				stringMap.insert("port0",@bport);
				stringMap.insert("worldid0",""+@wid);
				stringMap.insert("name0","BattleServer" + @sid);
				@sport = cfgTable.getValueByKey(@serverIndex,"端口");
				stringMap.insert("ServerPort",@sport);
				stringMap.insert("ServerIp", @ip0);
		};
	};
	if(serverType=="database"){
		stringMap.insert("name","DatabaseServer");
		stringMap.insert("centernum","1");
		stringMap.insert("ip0", @ip1);
		stringMap.insert("port0", @port);
		stringMap.insert("worldid0",""+@wid);
		stringMap.insert("name0","DatabaseServer");
	};
  
	stateName=cfgTable.getValueByKey(@rIndex,"状态");
	
	handleState();
	
	log("配置项处理完成：");
	stringMap.listMap(logFile);
	return 1;
};

return main();
